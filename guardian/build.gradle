plugins {
    id "com.auth0.gradle.oss-library.android" version "0.15.1"
}

logger.lifecycle("Using version ${version} for ${name}")

oss {
    name 'Guardian.Android'
    repository 'Guardian.Android'
    organization 'auth0'
    description 'Android toolkit for Auth0 Guardian API'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        nikolaseu {
            displayName = 'Nicolas Ulrich'
            email = 'nicolas.ulrich@auth0.com'
        }
    }
}

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode 1
        versionName project.version
        buildConfigField "String", "VERSION_NAME", "\"${project.version}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            all {
                maxHeapSize = "1024m"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Annotations (@NonNull etc)
    implementation 'com.android.support:support-annotations:28.0.0'
    //Gson
    implementation 'com.google.code.gson:gson:2.8.6'
    //OkHttp
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.0'
    //Mockito
    testImplementation 'org.mockito:mockito-core:3.6.28'
    //Hamcrest matchers
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    //OkHttp MockWebServer
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.14.0'
    //Awaitility
    testImplementation 'com.jayway.awaitility:awaitility:1.7.0'
    //Robolectric
    testImplementation 'org.robolectric:robolectric:4.4'
    // JWT verification
    testImplementation 'com.auth0:java-jwt:2.3.0'
    testImplementation 'junit:junit:4.13.2'
}
